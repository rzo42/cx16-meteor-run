10 REM "METEOR RUN BY RYAN LISTON 2019"

20 REM SETS UP A DIMINISHED ARRAY WITH 255 STRINGS
30 DIMM$(255)

40 REM "SETS SCREEN UP FOR INTRO SCREEN"
50 REM "POKE 646 SETS PRINT COLOR, CHR 147 CLEARS SCREEN"
60 REM "CHR 14 SETS TEXT TO UPPER/LOWER CASE SET"
70 REM "VPOKE $F,1 AND 2 SETS HORIZONTAL AND VERTICAL SCREEN RESOLUTION"

80 COLOR0:PRINTCHR$(1):COLOR5
90PRINTCHR$(147)CHR$(14);:VPOKE$F,1,89:VPOKE$F,2,58

100 REM "PRINTS INTRO TEXT"

110 PRINT"":PRINT" wELCOME FLYBOY,":PRINT""
120 PRINT" i'VE BEEN TOLD YOUR THE BEST PILOT IN THE QUADRANT..."
130 PRINT" lETS HOPE THAT'S TRUE.":PRINT""
140 PRINT" wE HAVE A COLONY ON A SMALL MOON AROUND PLANET vERA c"
150 PRINT"   THAT REQUIRES A DAILY SHIPMENT OF SUPPLIES. ":PRINT""
160 PRINT" fOR THE NEXT 7 DAYS THERE WILL BE A LARGE METEOR"
170 PRINT"   FIELD PASSING BETWEEN US AND THEM. eVRY DAY THE "
180 PRINT"   FIELD WILL GROW FASTER AND MORE DENSE.":PRINT""
190 PRINT" yOU WILL BE PILOTING THE x16 DELIVERY SHUTTLE."
200 PRINT" a SHIP KNOWN FOR IT'S SWIFT MANEUVERABILITY.":PRINT""
210 PRINT" tHE JOB PAYS 100,000 CREDITS FOR THE FIRST DAY."
220 PRINT" 10% WILL BE ADDED EACH DAY. tHERE IS ALSO A TIME"
230 PRINT" BONUS FOR FINISHING EACH MISSION AHEAD OF SHEDULE."
240 PRINT" dON'T GET TO GREEDY, YOU ONLY HAVE ONE LIFE.":PRINT"
250 PRINT" iF YOU SURVIVE YOU'LL BE ABLE TO RETIRE OFF OF 7"
260 PRINT"   DAYS OF WORK.":PRINT""
270 PRINT" gOOD LUCK OUT THERE AND STAY ALERT, i'D HATE TO"
280 PRINT"   LOSE A SHIP."

290 REM "GENERATE PONTLESS RANDOM RANDOM NUMEBERS"
300 REM "MAKES FIRST METEOR FIELD LESS LESS PREDICTABLE"

310 X=INT(RND(1)*9999)+0

320 REM "CALLS GETJOY AND LOOPS UNLESS BUTTON PRESS"

330 IFJOY(1)=16THEN360
340 GOTO310

350 REM "SETS X TO 0"

360 X=0

370 REM "SCREEN SETUP"

380 REM "CHR 142 SETS TEXT TO UPPER CASE/SYMBOL SET"

390 PRINT CHR$(147)CHR$(142);
400 VPOKE $F, 1, 32
410 VPOKE $F, 2, 39

420 REM "SETS BORDER COLOR"

430 VPOKE $F, 3, $C3

440 REM "SETS H START LOW BYTE"

450 VPOKE $F, 4, 188

460 REM "SETS H STOP LOW BYTE"

470 VPOKE $F, 5, 187

480 REM "SETS V START LOW BYTE"

490 VPOKE $F, 6, 016

500 REM "SETS V STOP LOW BYTE"

510 VPOKE $F, 7, 208

520 REM "SETS H/V START AND STOP HIGH NIBLES"

530 VPOKE $F, 8, %00100100

540REM "GAME LOAD SCREEN"

550 PRINT CHR$(147)"METEOR":PRINT "RUN":PRINT""
560 PRINT "BY":PRINT"":PRINT "RYAN":PRINT "LISTON":PRINT"":PRINT"2019":PRINT""
570 PRINT "U D L R":PRINT"TO MOVE":PRINT""
580 PRINT"U D ":PRINT"CHANGES":PRINT"SPEED"




590 REM "SPACE SHIP GFX"
600 DATA $20,$A0,$02,$0A,$F0,$F0,$0F,$0F,$F1,$F1,$1F,$1F,$F1,$F1,$1F,$1F
610 DATA $F0,$F1,$1F,$0F,$00,$FE,$EF,$00,$00,$0E,$E0,$00,$00,$01,$10,$00



620 REM "LOADS SPRITE GFX"

630 FORT=0TO31:READA:VPOKE1,T,A:NEXTT:RESTORE




640 REM "SETS SPRITE ATTRIBUTES"

650 REM "SHIP"

660 REM " SETS ADRESS LOW BYTE"

670 VPOKE $F,$5000,%00000000

680 REM "SETS MODE AND ADRESS HIGH NIBBLE"

690 VPOKE $F,$5001,%00001000


700 REM "X LOW BYTE"

710 VPOKE $F,$5002,28

720 REM "X HIGH BYTE"

730 VPOKE $F,$5003,0

740 REM "Y LOW BYTE"

750 VPOKE $F,$5004,8

760 REM " Y HIGH BYTE

770 VPOKE $F,$5005,0

780 REM "SETS Z DEPTH LAYER"

790 VPOKE $F,$5006,%00001000

800 REM "SETS SPRITE SIZE AND PALLETE OFFSET"

810 VPOKE $F,$5007,0

820 REM " TURNS SPRITE ON/OFF, SETS DAY TO 1 AND SCORE TO 0"

830 VPOKE $F,$4000,0:D=1:SC=0








840 REM "GENERATE METREOR FIELD"
850 M=0:X1=28:Y1=8:XX=0:XY=0
860 FORT=0TO255:M$(T)=""
870 FORT2=0TO7
880 X=INT(RND(1)*(40-(D/1.3)-M))+0
890 IFX<1THENM2$="q":GOTO910
900 M2$=" "
910 M$(T)=M$(T)+M2$:NEXTT2:M=M+.08:NEXTT
920 COLOR9:PRINTCHR$(147);


930 REM "GAME LOOP"
940 VPOKE$F,$4000,1:T=0
950 PRINTCHR$(147);:FORT=0TO16:IFT=12THENPRINT" DAY "D"":NEXTT
960 PRINT"":NEXTT
970 FORT=0TO30:PRINTM$(INT(RND(1)*255)+0)"":NEXTT

980 REM "ANIMATES METEOR FIELD"

990 PRINTM$(INT(RND(1)*255)+0)"":FORT=.TO10-(Y1/16)-(D*1.01):XY=XY+1

1000 REM "ANIMATES SPRITE WITH H FLIP"
1010 IFVPEEK($F,$5006)=8THENVPOKE$F,$5006,9:GOTO1050
1020 IFVPEEK($F,$5006)=9THENVPOKE$F,$5006,8

1030 REM"CONTROL"

1040 REM" GETS JOYSTIC INPUT"

1050 GJ=JOY(1)

1060 REM "TEST FOR DIRECTIONAL BUTTON PRESS"

1070 IFGJ=1THENX1=X1+1.5:IFX1>56THENX1=56
1080 IFGJ=2THENX1=X1-1.5:IFX1<0THENX1=0
1090 IFGJ=4THENY1=Y1+1.5:IFY1>128THENY1=128
1100 IFGJ=8THENY1=Y1-1.5:IFY1<0THENY1=0

1110 REM "MOVES SPRITE"

1120 VPOKE$F,$5002,X1:VPOKE$F,$5004,Y1

1130 REM "TEST FOR METEOR COLLISION"

1140 IFVPEEK(0,(INT((X1+1)/8)+(INT((Y1+1)/8)*128))*2)=81THEN1220
1150 IFVPEEK(0,(INT((X1+1)/8)+(INT((Y1+6)/8)*128))*2)=81THEN1220
1160 IFVPEEK(0,(INT((X1+6)/8)+(INT((Y1+1)/8)*128))*2)=81THEN1220
1170 IFVPEEK(0,(INT((X1+6)/8)+(INT((Y1+6)/8)*128))*2)=81THEN1220

1180 REM "CONTOLS LEVEL LENGTH"

1190 NEXTT:XX=XX+1:IFXX>255+(D*5)THEN1280
1200 GOTO990

1210 REM "CRASH ANIMATION SCROLLS PALLET OFFSET"

1220 T=20:NEXTT:FORT=0TO15:VPOKE$F,$5007,T:FORT2=0TO1000:NEXTT2:NEXTT

1230 REM "TURNS SPRITE OFF AND CLEARS SCREEN"

1240 VPOKE$F,$5007,0:VPOKE$F,$4000,0:POKE646,5:PRINTCHR$(147)

1250 REM "GAME OVER SCREEN"

1260 PRINT"":PRINT"  GAME":PRINT"":PRINT"  OVER":FORT=0TO15000:NEXTT:GOTO550

1270 REM "LEVEL COMPLETE SCREEN"

1280 PRINTCHR$(147):VPOKE$F,$4000,0
1290 PRINT"":PRINT" DAY "D"":PRINT"":PRINT"COMPLETE"
1300 SC=SC+100000+((D*1000)-1000):PRINT"RECIVED":PRINT100000+((D*1000)-1000)""
1310 PRINT"TIME":PRINT"BOUNUS"
1320 PRINT2350-XY-(D*5):SC=SC+2350-XY-(D*5):PRINT"TOTAL":PRINTSC"":FORT=0TO8000
1330 NEXTT

1340 REM "TEST FOR GAME COMPLETE"

1350 IFD>6THENPRINTCHR$(147)CHR$(14);:COLOR5:GOTO1400

1360REM "INCREMENTS DAY (LEVEL)

1370 D=D+1:GOTO840


1380 REM "VICTORY"
1390 REM "SETS UP VICTORY SCREEN"

1400 VPOKE $F,1,89
1410 VPOKE $F,2,58

1420 VPOKE $F,4,0
1430 VPOKE $F,5,%10000000
1440 VPOKE$F,6,0
1450 VPOKE$F,7,%11100000
1460 VPOKE$F,8,%00101000

1470 REM "PRINTS VICTORY SCREEN"

1480 PRINT:PRINT:PRINT:PRINT:PRINT"":PRINT"cONGRATULATIONS FLY BOY,"
1490 PRINT"  YOU SURVIVED THE MISSION.":PRINT""
1500 PRINT"THANKS TO YOU HUNDREDS OF COLONIST WERE ABLE"
1510 PRINT"  RECIEVE THE SUPPLIES THEY NEED TO SURVIVE.":PRINT""
1520 PRINT"hERE IS YOUR PAY AS PROMISED.":PRINT"":PRINT"RECIEVED,":PRINT""
1530 PRINTSC" CREDITS":PRINT""
1540 PRINT"outstanding job!":PRINT""
1550 PRINT"iF i EVER NEED ANOTHER GOOD PILOT i'LL"
1560 PRINT"  GIVE YOU A CALL."

1570 REM "RESTARTS GAME"

1580 FORT=0TO200000:NEXTT:GOTO80