1 DIMM$(255)
2 POKE646,$05:PRINTCHR$(147)CHR$(14);:VPOKE$F,1,89:VPOKE$F,2,58
3 PRINT"":PRINT" wELCOME FLYBOY,":PRINT""
4 PRINT" i'VE BEEN TOLD YOUR THE BEST PILOT IN THE QUADRANT..."
5 PRINT" lETS HOPE THAT'S TRUE.":PRINT""
6 PRINT" wE HAVE A COLONY ON A SMALL MOON AROUND PLANET vERA c"
7 PRINT"   THAT REQUIRES A DAILY SHIPMENT OF SUPPLIES. ":PRINT""
8 PRINT" fOR THE NEXT 7 DAYS THERE WILL BE A LARGE METEOR"
9 PRINT"   FIELD PASSING BETWEEN US AND THEM. eVRY DAY THE "
10 PRINT"   FIELD WILL GROW FASTER AND MORE DENSE.":PRINT""
11 PRINT" yOU WILL BE PILOTING THE x16 DELIVERY SHUTTLE."
12 PRINT" a SHIP KNOWN FOR IT'S SWIFT MANEUVERABILITY.":PRINT""
13 PRINT" tHE JOB PAYS 100,000 CREDITS FOR THE FIRST DAY."
14 PRINT" 10% WILL BE ADDED EACH DAY. tHERE IS ALSO A TIME"
15 PRINT" BONUS FOR FINISHING EACH MISSION AHEAD OF SHEDULE."
16 PRINT" dON'T GET TO GREEDY, YOU ONLY HAVE ONE LIFE.":PRINT"
17 PRINT" iF YOU SURVIVE YOU'LL BE ABLE TO RETIRE OFF OF 7"
18 PRINT"   DAYS OF WORK.":PRINT""
19 PRINT" gOOD LUCK OUT THERE AND STAY ALERT, i'D HATE TO"
20 PRINT"   LOSE A SHIP."
21 X=INT(RND(1)*9999)+0:SYS $FF06:IFPEEK($02BC)=255ORPEEK($02BC)=239THEN21
22 X=0:PRINTCHR$(147)CHR$(142);:VPOKE$F,1, 32:VPOKE $F, 2, 39:VPOKE $F, 3, $C3
23VPOKE$F,4,188:VPOKE $F,5,187:VPOKE$F,6,016:VPOKE$F,7,208:VPOKE$F,8,%00100100
24 PRINT CHR$(147)"METEOR":PRINT "RUN":PRINT""
25 PRINT "BY":PRINT"":PRINT "RYAN":PRINT "LISTON":PRINT"":PRINT"2019":PRINT""
26PRINT"U D L R":PRINT"TO MOVE":PRINT"":PRINT"U D ":PRINT"CHANGES":PRINT"SPEED"
27 DATA $20,$A0,$02,$0A,$F0,$F0,$0F,$0F,$F1,$F1,$1F,$1F,$F1,$F1,$1F,$1F
28 DATA $F0,$F1,$1F,$0F,$00,$FE,$EF,$00,$00,$0E,$E0,$00,$00,$01,$10,$00
29 FORT=0TO31:READA:VPOKE1,T,A:NEXTT:RESTORE
30VPOKE$F,$5000,%00000000:VPOKE $F,$5001,%00001000:VPOKE$F,$5002,28
31VPOKE $F,$5003,0:VPOKE $F,$5004,8:VPOKE $F,$5005,0:VPOKE $F,$5006,%00001000
32VPOKE $F,$5007,0:VPOKE $F,$4000,0:D=1:SC=0
33 M=0:X1=28:Y1=8:XX=0:XY=0:FORT=0TO255:M$(T)="":FORT2=0TO7
34 X=INT(RND(1)*(38-(D/1.3)-M))+0:IFX<1THENM2$="q":GOTO36
35 M2$=" "
36 M$(T)=M$(T)+M2$:NEXTT2:M=M+.08:NEXTT:POKE646,$09:PRINTCHR$(147);
37 VPOKE$F,$4000,1:T=0
38 PRINTCHR$(147);:FORT=0TO16:IFT=12THENPRINT" DAY "D"":NEXTT
39 PRINT"":NEXTT:FORT=0TO30:PRINTM$(INT(RND(1)*255)+0)"":NEXTT
40 PRINTM$(INT(RND(1)*255)+0)"":FORT=0TO10-(Y1/16)-(D*1.01):XY=XY+1
41 IFVPEEK($F,$5006)=8THENVPOKE$F,$5006,9:GOTO43
42 IFVPEEK($F,$5006)=9THENVPOKE$F,$5006,8
43 SYS $FF06:GJ=PEEK($02BC):IFGJ=%11111110THENX1=X1+1:IFX1>56THENX1=56
44 IFGJ=%11111101THENX1=X1-1:IFX1<0THENX1=0
45 IFGJ=%11111011THENY1=Y1+1:IFY1>128THENY1=128
46 IFGJ=%11110111THENY1=Y1-1:IFY1<0THENY1=0
47 VPOKE$F,$5002,X1:VPOKE$F,$5004,Y1
48 IFVPEEK(0,(INT((X1+1)/8)+(INT((Y1+1)/8)*128))*2)=81THEN54
49 IFVPEEK(0,(INT((X1+1)/8)+(INT((Y1+6)/8)*128))*2)=81THEN54
50 IFVPEEK(0,(INT((X1+6)/8)+(INT((Y1+1)/8)*128))*2)=81THEN54
51 IFVPEEK(0,(INT((X1+6)/8)+(INT((Y1+6)/8)*128))*2)=81THEN54
52 NEXTT:XX=XX+1:IFXX>255+(D*5)THEN57
53 GOTO40
54 T=20:NEXTT:FORT=0TO15:VPOKE$F,$5007,T:FORT2=0TO1000:NEXTT2:NEXTT
55 VPOKE$F,$5007,0:VPOKE$F,$4000,0:POKE646,5:PRINTCHR$(147)
56 PRINT"":PRINT"  GAME":PRINT"":PRINT"  OVER":FORT=0TO15000:NEXTT:GOTO24
57 PRINTCHR$(147):VPOKE$F,$4000,0
58 PRINT"":PRINT" DAY "D"":PRINT"":PRINT"COMPLETE"
59 SC=SC+100000+((D*1000)-1000):PRINT"RECIVED":PRINT100000+((D*1000)-1000)""
60 PRINT"TIME":PRINT"BOUNUS"
61 PRINT2350-XY-(D*5):SC=SC+2350-XY+(D*5):PRINT"TOTAL":PRINTSC"":FORT=0TO8000
62 NEXTT:IFD>6THENPRINTCHR$(147)CHR$(14);:POKE646,$05:GOTO64
63 D=D+1:GOTO33
64 VPOKE $F,1,89:VPOKE $F,2,58:VPOKE $F,4,0:VPOKE $F,5,%10000000
65 VPOKE$F,6,0:VPOKE$F,7,%11100000:VPOKE$F,8,%00101000
66 PRINT:PRINT:PRINT:PRINT:PRINT"":PRINT"cONGRATULATIONS FLY BOY,"
67 PRINT"  YOU SURVIVED THE MISSION.":PRINT""
68 PRINT"THANKS TO YOU HUNDREDS OF COLONIST WERE ABLE"
69 PRINT"  RECIEVE THE SUPPLIES THEY NEED TO SURVIVE.":PRINT""
70 PRINT"hERE IS YOUR PAY AS PROMISED.":PRINT"":PRINT"RECIEVED,":PRINT""
71 PRINTSC" CREDITS":PRINT"":PRINT"outstanding job!":PRINT""
72 PRINT"iF i EVER NEED ANOTHER GOOD PILOT i'LL":PRINT"  GIVE YOU A CALL."
73 FORT=0TO200000:NEXTT:GOTO2