
10 REM "METEOR RUN BY RYAN LISTON 2019"

20 REM "SETS UP A DIMINISHED ARRAY WITH 255 STRINGS"
30 DIMM$(255):A1=PEEK($9F25):A2=PEEK($9F29):
35 VPOKE1,$F9C2,%01010100:VPOKE1,$F9C3,%11001111
36 VPOKE1,$F9C6,%10010101:VPOKE1,$F9C7,%11000111
37GOTO290



40 BT=255:BY=BT+1:CB=$A040:PB=$A240:RB=$A008:F1=$9FE0:F2=F1+1
50 FORT=0TO7:CT(T)=CB+(T*64):PT(T)=PB+(T*128):NEXTT
60 FORT=0TO6:FORT2=0TO7:POKEF1,$20+($20*T)+T2:POKEF2,PEEK(RB+(T*8)+T2)
70 NEXTT2:NEXTT
80 CL=PEEK(RB-8):PL=PEEK(RB-7):NL=PEEK(RB-6):ND=TI+NL
90 CC=0:PC=0:RETURN

100 VC=0
110 NV=PEEK(PT(VC)+PC+PEEK(CT(VC)+CC)):IFNV>1THENPOKEF1,$28+VC:POKEF2,NV
120 IFVC<7THENVC=VC+1:GOTO110
130 VC=0:POKEF1,$08
140 NV=PEEK(PT(VC)+PC+PEEK(CT(VC)+CC)):IFNV>1THENPOKEF2,$08+VC
150 IFVC<7THENVC=VC+1:GOTO140
160 IFTI<NDTHEN160

170 ND=TI+NL
180 VC=0:POKEF1,$08

190 NV=PEEK(PT(VC)+PC+PEEK(CT(VC)+CC)):IFNOTNV=1THENPOKEF2,$00+VC
200 IFVC<7THENVC=VC+1:GOTO190

210 IFPC=PLTHENPC=0:GOTO230
220 PC=PC+1:RETURN
230 IFCC=CLTHENCC=0:RETURN
240 CC=CC+1:RETURN





250 REM "SETS SCREEN UP FOR INTRO SCREEN"
260 REM "CHR 147 CLEARS SCREEN"
270 REM "CHR 14 SETS TEXT TO UPPER/LOWER CASE SET"
280 REM "VPOKE $F,1 AND 2 SETS HORIZONTAL AND VERTICAL SCREEN RESOLUTION"

290 COLOR5,0:POKE$9F25,A1:LOAD"TM.BIN",8,1,$A000:GOSUB40
300 PRINTCHR$(147);:POKE$9F2A,89:POKE$9F2B,58:COLOR0,$D

310 VLOAD"TS.BIN",8,0,0
320 GOSUB100

330 IFJOY(1)=16THEN350
340 GOTO320


350 REM "PRINTS INTRO TEXT"
360 REM "CHR 14 SETS TEXT TO UPPER/LOWER CASE SET"

370 COLOR5,0:PRINTCHR$(147)CHR$(14);
380 VLOAD"ST.BIN",8,0,0

390 REM "CALLS GETJOY AND LOOPS UNLESS BUTTON PRESS"

400 TX=TI+20
410 IFTI<TXTHEN410
420 IFJOY(1)=16THEN450
430 GOTO400

440 REM "SETS X TO 0"

450 X=0

460 REM "SCREEN SETUP"
470 REM "SCREEN0 SETS SCREEN TO 40*30"

480 REM "CHR 142 SETS TEXT TO UPPER CASE/SYMBOL SET"

490 SCREEN0:PRINT CHR$(147)CHR$(142);
500 POKE $9F25,A1
510 POKE $9F2A, 32
520 POKE $9F2B, 39

530 REM "SETS BORDER COLOR"

540 POKE $9F2C, $C3

550 REM "SETS H START LOW BYTE"

560 POKE$9F25,A1+2

570 POKE $9F29, 47

580 REM "SETS H STOP LOW BYTE"

590 POKE $9F2A, 111

600 REM "SETS V START LOW BYTE"

610 POKE $9F2B, 8

620 REM "SETS V STOP LOW BYTE"

630 POKE $9F2C, 232



640 REM "LOADS SPRITE GFX"

650 VLOAD"SP.BIN",8,1,0




660 REM "SETS SPRITE ATTRIBUTES"

670 REM "SHIP"

680 REM " SETS ADRESS LOW BYTE"

690 VPOKE $1,$FC00,%00000000

700 REM "SETS MODE AND ADRESS HIGH NIBBLE"

710 VPOKE $1,$FC01,%00001000


720 REM "X LOW BYTE"

730 VPOKE $1,$FC02,28

740 REM "X HIGH BYTE"

750 VPOKE $1,$FC03,0

760 REM "Y LOW BYTE"

770 VPOKE $1,$FC04,8

780 REM " Y HIGH BYTE

790 VPOKE $1,$FC05,0

800 REM "SETS Z DEPTH LAYER"

810 VPOKE $1,$FC06,%00001000

820 REM "SETS SPRITE SIZE AND PALLETE OFFSET"

830 VPOKE $1,$FC07,0




840REM "GAME LOAD SCREEN"

860 PRINT CHR$(147):FORT=0TO2:PRINT:NEXTT:COLOR7,0
880 PRINT "UP,DOWN,":PRINT"LEFT,":PRINT"RIGHT":PRINT"TO MOVE":PRINT
935 COLOR8,0:PRINT "UP,DOWN":PRINT"TO":PRINT"CHANGE":PRINT"SPEED"
990 COLOR$A,0:PRINT:PRINT:PRINT"LOADING":COLOR4,0:PRINT"........"
995LOAD"LM.BIN",8,1,$A000





1100 REM " TURNS SPRITE ON/OFF, SETS DAY TO 1 AND SCORE TO 0"

1110 POKE$9F25,A1:POKE$9F29,A2+64:D=1:SC=0








1120 REM "GENERATE METREOR FIELD"
1130 M=0:X1=28:Y1=8:XX=0:XY=0
1140 FORE=0TO255:M$(E)=""
1150 FORE2=0TO7
1160 X=INT(RND(1)*(40-(D/1.3)-M))+0
1170 IFX<1THENM2$="q":GOTO1190
1180 M2$=" "
1190 M$(E)=M$(E)+M2$:NEXTE2:M=M+.08:NEXTE
1200 COLOR9,0:PRINTCHR$(147);


1210 REM "GAME LOOP"
1220 POKE$9F25,A1:POKE$9F29,A2+64:T=0
1230 PRINTCHR$(147);:FORT=0TO16:IFT=12THENPRINT" DAY "D"":NEXTT
1240 PRINT"":NEXTT
1241 VPOKE1,$F9C0,128:VPOKE1,$F9C4,128

1250 REM "ANIMATES METEOR FIELD"

1260 PRINTM$(INT(RND(1)*255)+0)"":FORT=.TO10-(Y1/16)-(D*1.01):XY=XY+1
1265 VPOKE1,$F9C1,1+Y1/64:VPOKE1,$F9C5,8+Y1/32
1266 VPOKE1,$F9C0,INT(RND(1)*Y1/2)+X1:VPOKE1,$F9C4,INT(RND(1)*Y1/2)+X1

1270 REM "ANIMATES SPRITE WITH H FLIP"
1280 IFVPEEK($1,$FC06)=8THENVPOKE$1,$FC06,9:GOTO1320
1290 IFVPEEK($1,$FC06)=9THENVPOKE$1,$FC06,8

1300 REM"CONTROL"

1310 REM" GETS JOYSTIC INPUT"

1320 GJ=JOY(1)

1330 REM "TEST FOR DIRECTIONAL BUTTON PRESS"

1340 IFGJ=1THENX1=X1+1.5:IFX1>56THENX1=56
1350 IFGJ=2THENX1=X1-1.5:IFX1<0THENX1=0
1360 IFGJ=4THENY1=Y1+1.5:IFY1>128THENY1=128
1370 IFGJ=8THENY1=Y1-1.5:IFY1<0THENY1=0

1380 REM "MOVES SPRITE"

1390 VPOKE$1,$FC02,X1:VPOKE$1,$FC04,Y1

1400 REM "TEST FOR METEOR COLLISION"

1410 IFVPEEK(0,(INT((X1+1)/8)+(INT((Y1+1)/8)*128))*2)=81THEN1490
1420 IFVPEEK(0,(INT((X1+1)/8)+(INT((Y1+6)/8)*128))*2)=81THEN1490
1430 IFVPEEK(0,(INT((X1+6)/8)+(INT((Y1+1)/8)*128))*2)=81THEN1490
1440 IFVPEEK(0,(INT((X1+6)/8)+(INT((Y1+6)/8)*128))*2)=81THEN1490

1450 REM "CONTOLS LEVEL LENGTH"

1460 NEXTT:XX=XX+1:IFXX>255+(D*5)THEN1600
1470 GOTO1260

1480 REM "CRASH ANIMATION SCROLLS PALLET OFFSET"

1490 T=20:NEXTT:FORT=0TO15:VPOKE$1,$FC07,T:TX=TI+15
1500 IFTI<TXTHEN1500
1510 VPOKE1,$F9C1,INT(RND(1)*8)+10:VPOKE1,$F9C5,INT(RND(1)*7)+15:NEXTT
1515 VPOKE1,$F9C1,0:VPOKE1,$F9C5,0:VPOKE1,$F9C0,0:VPOKE1,$F9C4,0

1520 REM "TURNS SPRITE OFF AND CLEARS SCREEN"

1530 VPOKE$1,$FC07,0:POKE$9F25,A1:POKE$9F29,A2
1540 PRINTCHR$(147)

1550 REM "GAME OVER SCREEN"

1560 PRINT"":COLOR2,0:PRINT"  GAME":PRINT"":PRINT"  OVER":TX=TI+300
1570 IFTI<TXTHEN1570
1580 GOTO450

1590 REM "LEVEL COMPLETE SCREEN"

1600 PRINTCHR$(147):POKE$9F25,A1:POKE$9F29,A2:COLOR$E,0
1605 VPOKE1,$F9C1,0:VPOKE1,$F9C5,0:VPOKE1,$F9C0,0:VPOKE1,$F9C4,0
1610 PRINT"":PRINT"DAY "D"":PRINT"":PRINT"COMPLETE":PRINT
1620 SC=SC+100000+((D*1000)-1000):PRINT"RECIVED":PRINT100000+((D*1000)-1000)""
1630 PRINT:PRINT"TIME":PRINT"BOUNUS"
1640 PRINT2350-XY-(D*5)"":SC=SC+2350-XY-(D*5)
1643 PRINT:PRINT"TOTAL":PRINTSC"":TX=TI+300
1650 IFTI<TXTHEN1650

1660 REM "TEST FOR GAME COMPLETE"

1670 IFD>6THENPRINTCHR$(147)CHR$(14);:COLOR5,0:GOTO1720

1680REM "INCREMENTS DAY (LEVEL)

1690 D=D+1:GOTO1120


1700 REM "VICTORY"
1710 REM "SETS UP VICTORY SCREEN"

1720POKE$9F25,A1
1725 VPOKE1,$F9C1,0:VPOKE1,$F9C5,0:VPOKE1,$F9C0,0:VPOKE1,$F9C4,0

1730 POKE $9F2A,89
1740 POKE $9F2B,58

1750 POKE$9F25,A1+2

1760 POKE $9F29,0
1770 POKE $9F2A,96
1780 POKE$9F2B,0
1790 POKE$9F2C,240

1800 REM "PRINTS VICTORY SCREEN"
1805COLOR5,0
1810 PRINT:PRINT:PRINT:PRINT:PRINT"":PRINT"cONGRATULATIONS FLY BOY,"
1820 PRINT"  YOU SURVIVED THE MISSION.":PRINT""
1830 PRINT"THANKS TO YOU HUNDREDS OF COLONIST WERE ABLE"
1840 PRINT"  RECIEVE THE SUPPLIES THEY NEED TO SURVIVE.":PRINT""
1850 PRINT"hERE IS YOUR PAY AS PROMISED.":PRINT"":PRINT"RECIEVED,":PRINT""
1860 PRINTSC" CREDITS":PRINT""
1870 PRINT"outstanding job!":PRINT""
1880 PRINT"iF i EVER NEED ANOTHER GOOD PILOT i'LL"
1890 PRINT"  GIVE YOU A CALL."

1900 REM "RESTARTS GAME"

1910 GOSUB100
1920 IFJOY(1)=16THEN290
1930 GOTO1910