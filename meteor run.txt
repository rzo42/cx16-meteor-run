10 POKE646,$05:PRINTCHR$(147)CHR$(14);:VPOKE$F,1,89:VPOKE$F,2,58
20 PRINT:PRINT" wELCOME FLYBOY,":PRINT
30 PRINT" i'VE BEEN TOLD YOUR THE BEST PILOT IN THE QUADRANT..."
40 PRINT" lETS HOPE THAT'S TRUE.":PRINT
50 PRINT" wE HAVE A COLONY ON A SMALL MOON AROUND PLANET vERA c
60 PRINT"   THAT REQUIRES A DAILY SHIPMENT OF SUPPLIES. ":PRINT
70 PRINT" fOR THE NEXT 14 DAYS THERE WILL BE A LARGE METEOR"
80 PRINT"   FIELD PASSING BETWEEN US AND THEM. eVRY DAY THE "
90 PRINT"   FIELD WILL GROW FASTER AND MORE DENSE.":PRINT
100 PRINT" yOU WILL BE PILOTING THE x16 DELIVERY SHUTTLE."
110 PRINT" a SHIP KNOWN FOR IT'S SWIFT MANEUVERABILITY.":PRINT
120 PRINT" tHE JOB PAYS 100,000 CREDITS FOR THE FIRST DAY.
130 PRINT" 10% WILL BE ADDED EACH DAY. tHERE IS ALSO A TIME"
140 PRINT" BONUS FOR FINISHING EACH MISSION AHEAD OF SHEDULE."
150 PRINT" dON'T GET TO GREEDY, YOU ONLY HAVE ONE LIFE.":PRINT
160 PRINT" iF YOU SURVIVE YOU'LL BE ABLE TO RETIRE OFF OF 14"
170 PRINT"   DAYS OF WORK.":PRINT
180 PRINT" gOOD LUCK OUT THERE AND STAY ALERT, i'D HATE TO
190 PRINT"   LOSE A SHIP."
200 X=INT(RND(1)*9999)+0:SYS $FF06:IFPEEK($EF)=255ORPEEK($EF)=239THEN200
210DIMM$(255):X=0

220 REM "SCREEN SETUP"

230 PRINT CHR$(147)CHR$(142);

240 VPOKE $F, 1, 32
250 VPOKE $F, 2, 39
260 VPOKE $F, 3, $C3
270 VPOKE $F, 4, 188
280 VPOKE $F, 5, 187
290 VPOKE $F, 6, 016
300 VPOKE $F, 7, 208
310 VPOKE $F, 8, %00100100

320 PRINT CHR$(147)"METEOR":PRINT "RUN":PRINT
330 PRINT "BY":PRINT:PRINT "RYAN":PRINT "LISTON":PRINT:PRINT "2019":PRINT
340 PRINT "U D L R":PRINT"TO MOVE":PRINT
350 PRINT"U D ":PRINT"CHANGES":PRINT"SPEED"
 



370 REM "SPACE SHIP GFX"
380 DATA $20,$A0,$02,$0A,$F0,$F0,$0F,$0F,$F1,$F1,$1F,$1F,$F1,$F1,$1F,$1F
390 DATA $F0,$F1,$1F,$0F,$00,$FE,$EF,$00,$00,$0E,$E0,$00,$00,$01,$10,$00



400 REM "LOADS SPRITE GFX"
410 FORT=0TO31:READA:VPOKE1,T,A:NEXTT:RESTORE




420 REM "SETS SPRITE ATTRIBUTES"

430 REM "SHIP"

440 VPOKE $F,$5000,%00000000
450 VPOKE $F,$5001,%00001000
460 VPOKE $F,$5002,28
470 VPOKE $F,$5003,0
480 VPOKE $F,$5004,8
490 VPOKE $F,$5005,0
500 VPOKE $F,$5006,%00001000
510 VPOKE $F,$5007,0
520 VPOKE $F,$4000,0:D=1:SC=0








530 REM "GENERATE METROER FIELD"
540 M=0:X1=28:Y1=8:XX=0:XY=0
550 FORT=0TO255
560 FORT2=0TO7
570 X=INT(RND(1)*(69-(D*2)-M))+0
580 IFX<1THENM2$="q":GOTO600
590 M2$=" "
600 M$(T)=M$(T)+M2$
610 NEXTT2
620 M=M+0.15:NEXTT
625POKE646,$09:PRINTCHR$(147)


640REM "TEST"
650 VPOKE$F,$4000,1
660 PRINTCHR$(147):FORT=0TO16:IFT=12THENPRINT" DAY "D:GOTO680
670 PRINT
680 NEXTT:FORT=0TO30:PRINTM$(INT(RND(1)*255)+0):NEXTT


690 PRINTM$(INT(RND(1)*255)+0):FORT=0TO20-(Y1/8)-(D*1.2):XY=XY+1

700 IFVPEEK($F,$5006)=8THENVPOKE$F,$5006,9:GOTO730
710 IFVPEEK($F,$5006)=9THENVPOKE$F,$5006,8

720REM"CONTROL"

730SYS $FF06:GJ=PEEK($EF)
750IFGJ=%11111110THENX1=X1+1:IFX1>56THENX1=56
760IFGJ=%11111101THENX1=X1-1:IFX1<0THENX1=0
770IFGJ=%11111011THENY1=Y1+1:IFY1>128THENY1=128
780IFGJ=%11110111THENY1=Y1-1:IFY1<0THENY1=0

790VPOKE$F,$5002,X1:VPOKE$F,$5004,Y1

800 IFVPEEK(0,(INT((X1+1)/8)+(INT((Y1+1)/8)*128))*2)=81THEN860
810 IFVPEEK(0,(INT((X1+1)/8)+(INT((Y1+6)/8)*128))*2)=81THEN860
820 IFVPEEK(0,(INT((X1+6)/8)+(INT((Y1+1)/8)*128))*2)=81THEN860
830 IFVPEEK(0,(INT((X1+6)/8)+(INT((Y1+6)/8)*128))*2)=81THEN860

840:NEXTT:XX=XX+1:IFXX>255THEN890
850GOTO690

860T=20:NEXTT:FORT=0TO15:VPOKE$F,$5007,T:FORT2=0TO1000:NEXTT2:NEXTT
870 VPOKE$F,$5007,0:VPOKE$F,$4000,0:POKE646,5:PRINTCHR$(147)
880 PRINT:PRINT"  GAME":PRINT:PRINT"  OVER":FORT=0TO15000:NEXTT:GOTO320


890PRINTCHR$(147):VPOKE$F,$4000,0
891:PRINT:PRINT" DAY "D:PRINT:PRINT"COMPLETE"
900SC=SC+100000+((D*1000)-1000):PRINT"RECIVED":PRINT100000+((D*1000)-1000)
901PRINT"TIME":PRINT"BOUNUS"
910 PRINT3500-XY:SC=SC+3500-XY:PRINT"TOTAL":PRINTSC:FORT=0TO10000:NEXTT
920 IFD=13THENPRINTCHR$(147)"YOU WIN":FORT=0TO10000:NEXTT:GOTO230
930 D=D+1:GOTO530