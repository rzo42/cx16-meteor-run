1 REM "METEOR RUN BY RYAN LISTON 2019"

2 REM SETS UP A DIMINISHED ARRAY WITH 255 STRINGS
3 DIMM$(255)

4 REM "SETS SCREEN UP FOR INTRO SCREEN"
5 REM "POKE 646 SETS PRINT COLOR, CHR 147 CLEARS SCREEN"
6 REM "CHR 14 SETS TEXT TO UPPER/LOWER CASE SET"
7 REM "VPOKE $F,1 AND 2 SETS HORIZONTAL AND VERTICAL SCREEN RESOLUTION"

8 POKE646,$05:PRINTCHR$(147)CHR$(14);:VPOKE$F,1,89:VPOKE$F,2,58

9 REM "PRINTS INTRO TEXT"

10 PRINT"":PRINT" wELCOME FLYBOY,":PRINT""
11 PRINT" i'VE BEEN TOLD YOUR THE BEST PILOT IN THE QUADRANT..."
12 PRINT" lETS HOPE THAT'S TRUE.":PRINT""
13 PRINT" wE HAVE A COLONY ON A SMALL MOON AROUND PLANET vERA c"
14 PRINT"   THAT REQUIRES A DAILY SHIPMENT OF SUPPLIES. ":PRINT""
15 PRINT" fOR THE NEXT 7 DAYS THERE WILL BE A LARGE METEOR"
16 PRINT"   FIELD PASSING BETWEEN US AND THEM. eVRY DAY THE "
17 PRINT"   FIELD WILL GROW FASTER AND MORE DENSE.":PRINT""
18 PRINT" yOU WILL BE PILOTING THE x16 DELIVERY SHUTTLE."
19 PRINT" a SHIP KNOWN FOR IT'S SWIFT MANEUVERABILITY.":PRINT""
20 PRINT" tHE JOB PAYS 100,000 CREDITS FOR THE FIRST DAY."
21 PRINT" 10% WILL BE ADDED EACH DAY. tHERE IS ALSO A TIME"
22 PRINT" BONUS FOR FINISHING EACH MISSION AHEAD OF SHEDULE."
23 PRINT" dON'T GET TO GREEDY, YOU ONLY HAVE ONE LIFE.":PRINT"
24 PRINT" iF YOU SURVIVE YOU'LL BE ABLE TO RETIRE OFF OF 7"
25 PRINT"   DAYS OF WORK.":PRINT""
26 PRINT" gOOD LUCK OUT THERE AND STAY ALERT, i'D HATE TO"
27 PRINT"   LOSE A SHIP."

28 REM "GENERATE PONTLESS RANDOM RANDOM NUMEBERS"
29 REM "MAKES FIRST METEOR FIELD LESS LESS PREDICTABLE"

30 X=INT(RND(1)*9999)+0

31 REM "CALLS GETJOY AND LOOPS UNLESS BUTTON PRESS"

32 SYS $FF06:IFPEEK($02BC)=255ORPEEK($02BC)=239THEN30

33 REM "SETS X TO 0"

34 X=0

35 REM "SCREEN SETUP"

36 REM "CHR 142 SETS TEXT TO UPPER CASE/SYMBOL SET"

37 PRINT CHR$(147)CHR$(142);
38 VPOKE $F, 1, 32
39 VPOKE $F, 2, 39

40 REM "SETS BORDER COLOR"

41 VPOKE $F, 3, $C3

42 REM "SETS H START LOW BYTE"

43 VPOKE $F, 4, 188

44 REM "SETS H STOP LOW BYTE"

45 VPOKE $F, 5, 187

46 REM "SETS V START LOW BYTE"

47 VPOKE $F, 6, 016

48 REM "SETS V STOP LOW BYTE"

49 VPOKE $F, 7, 208

50 REM "SETS H/V START AND STOP HIGH NIBLES"

51 VPOKE $F, 8, %00100100

52REM "GAME LOAD SCREEN"

53 PRINT CHR$(147)"METEOR":PRINT "RUN":PRINT""
54 PRINT "BY":PRINT"":PRINT "RYAN":PRINT "LISTON":PRINT"":PRINT"2019":PRINT""
55 PRINT "U D L R":PRINT"TO MOVE":PRINT""
56 PRINT"U D ":PRINT"CHANGES":PRINT"SPEED"




57 REM "SPACE SHIP GFX"
58 DATA $20,$A0,$02,$0A,$F0,$F0,$0F,$0F,$F1,$F1,$1F,$1F,$F1,$F1,$1F,$1F
59 DATA $F0,$F1,$1F,$0F,$00,$FE,$EF,$00,$00,$0E,$E0,$00,$00,$01,$10,$00



60 REM "LOADS SPRITE GFX"

61 FORT=0TO31:READA:VPOKE1,T,A:NEXTT:RESTORE




62 REM "SETS SPRITE ATTRIBUTES"

63 REM "SHIP"

64 REM " SETS ADRESS LOW BYTE"

65 VPOKE $F,$5000,%00000000

66 REM "SETS MODE AND ADRESS HIGH NIBBLE"

67 VPOKE $F,$5001,%00001000


68 REM "X LOW BYTE"

69 VPOKE $F,$5002,28

70 REM "X HIGH BYTE"

71 VPOKE $F,$5003,0

72 REM "Y LOW BYTE"

73 VPOKE $F,$5004,8

74 REM " Y HIGH BYTE

75 VPOKE $F,$5005,0

76 REM "SETS Z DEPTH LAYER"

77 VPOKE $F,$5006,%00001000

78 REM "SETS SPRITE SIZE AND PALLETE OFFSET"

79 VPOKE $F,$5007,0

80 REM " TURNS SPRITE ON/OFF, SETS DAY TO 1 AND SCORE TO 0"

81 VPOKE $F,$4000,0:D=1:SC=0








82 REM "GENERATE METREOR FIELD"
83 M=0:X1=28:Y1=8:XX=0:XY=0
84 FORT=0TO255:M$(T)=""
85 FORT2=0TO7
86 X=INT(RND(1)*(38-(D/1.3)-M))+0
87 IFX<1THENM2$="q":GOTO89
88 M2$=" "
89 M$(T)=M$(T)+M2$:NEXTT2:M=M+.08:NEXTT
90 POKE646,$09:PRINTCHR$(147);


91 REM "GAME LOOP"
92 VPOKE$F,$4000,1:T=0
93 PRINTCHR$(147);:FORT=0TO16:IFT=12THENPRINT" DAY "D"":NEXTT
94 PRINT"":NEXTT
95 FORT=0TO30:PRINTM$(INT(RND(1)*255)+0)"":NEXTT

96 REM "ANIMATES METEOR FIELD"

97 PRINTM$(INT(RND(1)*255)+0)"":FORT=0TO10-(Y1/16)-(D*1.01):XY=XY+1

98 REM "ANIMATES SPRITE WITH H FLIP"
99 IFVPEEK($F,$5006)=8THENVPOKE$F,$5006,9:GOTO103
100 IFVPEEK($F,$5006)=9THENVPOKE$F,$5006,8

101 REM"CONTROL"

102 REM" GETS JOYSTIC INPUT"

103 SYS $FF06:GJ=PEEK($02BC)

104 REM "TEST FOR DIRECTIONAL BUTTON PRESS"

105 IFGJ=%11111110THENX1=X1+1:IFX1>56THENX1=56
106 IFGJ=%11111101THENX1=X1-1:IFX1<0THENX1=0
107 IFGJ=%11111011THENY1=Y1+1:IFY1>128THENY1=128
108 IFGJ=%11110111THENY1=Y1-1:IFY1<0THENY1=0

109 REM "MOVES SPRITE"

110 VPOKE$F,$5002,X1:VPOKE$F,$5004,Y1

111 REM "TEST FOR METEOR COLLISION"

112 IFVPEEK(0,(INT((X1+1)/8)+(INT((Y1+1)/8)*128))*2)=81THEN120
113 IFVPEEK(0,(INT((X1+1)/8)+(INT((Y1+6)/8)*128))*2)=81THEN120
114 IFVPEEK(0,(INT((X1+6)/8)+(INT((Y1+1)/8)*128))*2)=81THEN120
115 IFVPEEK(0,(INT((X1+6)/8)+(INT((Y1+6)/8)*128))*2)=81THEN120

116 REM "CONTOLS LEVEL LENGTH"

117 NEXTT:XX=XX+1:IFXX>255+(D*5)THEN126
118 GOTO97

119 REM "CRASH ANIMATION SCROLLS PALLET OFFSET"

120 T=20:NEXTT:FORT=0TO15:VPOKE$F,$5007,T:FORT2=0TO1000:NEXTT2:NEXTT

121 REM "TURNS SPRITE OFF AND CLEARS SCREEN"

122 VPOKE$F,$5007,0:VPOKE$F,$4000,0:POKE646,5:PRINTCHR$(147)

123 REM "GAME OVER SCREEN"

124 PRINT"":PRINT"  GAME":PRINT"":PRINT"  OVER":FORT=0TO15000:NEXTT:GOTO53

125 REM "LEVEL COMPLETE SCREEN"

126 PRINTCHR$(147):VPOKE$F,$4000,0
127 PRINT"":PRINT" DAY "D"":PRINT"":PRINT"COMPLETE"
128 SC=SC+100000+((D*1000)-1000):PRINT"RECIVED":PRINT100000+((D*1000)-1000)""
129 PRINT"TIME":PRINT"BOUNUS"
130 PRINT2350-XY-(D*5):SC=SC+2350-XY+(D*5):PRINT"TOTAL":PRINTSC"":FORT=0TO8000
131 NEXTT

132 REM "TEST FOR GAME COMPLETE"

133 IFD>6THENPRINTCHR$(147)CHR$(14);:POKE646,$05:GOTO138

134REM "INCREMENTS DAY (LEVEL)

135 D=D+1:GOTO82


136 REM "VICTORY"
137 REM "SETS UP VICTORY SCREEN"

138 VPOKE $F,1,89
139 VPOKE $F,2,58

140 VPOKE $F,4,0
141 VPOKE $F,5,%10000000
142 VPOKE$F,6,0
143 VPOKE$F,7,%11100000
144 VPOKE$F,8,%00101000

145 REM "PRINTS VICTORY SCREEN"

146 PRINT:PRINT:PRINT:PRINT:PRINT"":PRINT"cONGRATULATIONS FLY BOY,"
147 PRINT"  YOU SURVIVED THE MISSION.":PRINT""
148 PRINT"THANKS TO YOU HUNDREDS OF COLONIST WERE ABLE"
149 PRINT"  RECIEVE THE SUPPLIES THEY NEED TO SURVIVE.":PRINT""
150 PRINT"hERE IS YOUR PAY AS PROMISED.":PRINT"":PRINT"RECIEVED,":PRINT""
151 PRINTSC" CREDITS":PRINT""
152 PRINT"outstanding job!":PRINT""
153 PRINT"iF i EVER NEED ANOTHER GOOD PILOT i'LL"
154 PRINT"  GIVE YOU A CALL."

155 REM "RESTARTS GAME"

156 FORT=0TO200000:NEXTT:GOTO8